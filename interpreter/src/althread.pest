/// # Althread Grammar
/// This file defines the grammar for the Althread programming language,

program = _{ SOI ~ blocks* ~ EOI }

/// ## Program Structure
/// The main building blocks of an Althread program are:
/// - **Main Block**: The entry point of the program.
/// - **Shared Block**: Declares global variables accessible across different parts of the program.
/// - **Condition Block**: Monitors conditions at each atomic step (e.g., always, never, eventually).
/// - **Process Block**: Encapsulates code that runs concurrently in parallel processes.
blocks = _{ main_block | global_block | condition_block | process_block }

global_block    = { GLOBAL_KW ~ "{" ~ statements ~ "}" }
condition_block = { condition_keywords ~ "{" ~ statements ~ "}" }
process_block   = { PROCESS_KW ~ identifier ~ "(" ~ ")" ~ "{" ~ statements ~ "}" }
main_block      = { MAIN_KW ~ "{" ~ statements ~ "}" }

condition_keywords = { ALWAYS_KW | NEVER_KW | EVENTUALLY_KW }

/// ## Statements
/// Statements are the executable instructions in the language.
/// They include assignments, declarations, expressions, print statements,
/// function calls, and control flow structures.
statements = { statement* }

statement             =  { assignment_statement | declaration_statement | expression_statement | print_statement | run_statement | if_control | while_control | scope }
assignment_statement  = _{ assignment ~ ";" }
declaration_statement = _{ declaration ~ ";" }
expression_statement  = _{ expression ~ ";" }
run_statement         = _{ run_call ~ ";" }
print_statement       = _{ print_call ~ ";" }

/// ### Assignments
/// Assignments assign values to variables.
/// - **Unary Assignments**: Increment or decrement a variable (e.g., a++).
/// - **Binary Assignments**: Assign the result of an expression to a variable (e.g., a = b + c).
assignment = { unary_assignment | binary_assignment }

unary_assignment          = { identifier ~ unary_assignment_operator }
unary_assignment_operator = { INC_OP | DEC_OP }

binary_assignment          = { identifier ~ binary_assignment_operator ~ expression }
binary_assignment_operator = { ASSIGN_OP | ADD_ASSIGN_OP | SUB_ASSIGN_OP | MUL_ASSIGN_OP | DIV_ASSIGN_OP | MOD_ASSIGN_OP }

/// ### Declarations
/// Declarations introduce new variables, which can be mutable (let) or immutable (const).
declaration         = { declaration_keyword ~ identifier ~ (":" ~ datatype)? ~ ("=" ~ expression)? }
declaration_keyword = { LET_KW | CONST_KW }

/// ### Expressions
/// Expressions evaluate values based on arithmetic and logical operations, following standard precedence rules.
expression = { binary_expression | unary_expression | primary_expression }

primary_expression = _{ literal | identifier | "(" ~ expression ~ ")" }

unary_expression = _{ unary_operator? ~ primary_expression }
unary_operator   =  { POS_OP | NEG_OP | NOT_OP }

binary_expression   = _{ unary_expression ~ (binary_operator ~ unary_expression)* }
binary_operator     = _{ or_operator | and_operator | equality_operator | comparison_operator | term_operator | factor_operator }
or_operator         =  { OR_OP }
and_operator        =  { AND_OP }
equality_operator   =  { EQ_OP | NE_OP }
comparison_operator =  { LT_OP | GT_OP | LE_OP | GE_OP }
term_operator       =  { ADD_OP | SUB_OP }
factor_operator     =  { MUL_OP | DIV_OP | MOD_OP }

/// ### Function Calls
/// Function calls include running parallel processes and printing to the console.
run_call   = { RUN_KW ~ identifier ~ "(" ~ ")" }
print_call = { PRINT_KW ~ "(" ~ expression ~ ")" }

/// ### Control Flow
/// Control flow structures include conditional execution and loops.
scope         = { "{" ~ statement* ~ "}" }
if_control    = { IF_KW ~ "(" ~ expression ~ ")" ~ scope ~ (ELSE_KW ~ scope)? }
while_control = { WHILE_KW ~ "(" ~ expression ~ ")" ~ scope }

/// ## Tokens
/// This section defines the keywords, operators, datatypes, and other tokens used in Althread.

/// ### Keywords
/// Keywords define the core constructs of the language.
KEYWORDS = _{ MAIN_KW | GLOBAL_KW | PROCESS_KW | ALWAYS_KW | NEVER_KW | EVENTUALLY_KW | LET_KW | CONST_KW | IF_KW | ELSE_KW | WHILE_KW | RUN_KW | PRINT_KW | BOOL | INT_TYPE | FLOAT_TYPE | STR_TYPE | VOID_TYPE }

MAIN_KW       = _{ "main" }
GLOBAL_KW     = _{ "shared" }
PROCESS_KW    = _{ "process" }
ALWAYS_KW     =  { "always" }
NEVER_KW      =  { "never" }
EVENTUALLY_KW =  { "eventually" }

LET_KW   = { "let" }
CONST_KW = { "const" }

IF_KW    = _{ "if" }
ELSE_KW  = _{ "else" }
WHILE_KW = _{ "while" }

RUN_KW   = _{ "run" }
PRINT_KW = _{ "print" }

TRUE_KW  = _{ "true" }
FALSE_KW = _{ "false" }
NULL_KW  = _{ "null" }

/// ### Operators
/// Operators are used for arithmetic, logical operations, and assignments.
POS_OP = { "+" }
NEG_OP = { "-" }
NOT_OP = { "!" }

ADD_OP = { "+" }
SUB_OP = { "-" }
MUL_OP = { "*" }
DIV_OP = { "/" }
MOD_OP = { "%" }

EQ_OP  = { "==" }
NE_OP  = { "!=" }
LT_OP  = { "<" }
GT_OP  = { ">" }
LE_OP  = { "<=" }
GE_OP  = { ">=" }
AND_OP = { "&&" }
OR_OP  = { "||" }

ASSIGN_OP     = { "=" }
ADD_ASSIGN_OP = { "+=" }
SUB_ASSIGN_OP = { "-=" }
MUL_ASSIGN_OP = { "*=" }
DIV_ASSIGN_OP = { "/=" }
MOD_ASSIGN_OP = { "%=" }

INC_OP = { "++" }
DEC_OP = { "--" }

/// ### Datatypes
/// Datatypes supported in Althread include boolean, integer, float, string, and void.
datatype   = { BOOL_TYPE | INT_TYPE | FLOAT_TYPE | STR_TYPE | VOID_TYPE }
BOOL_TYPE  = { "bool" }
INT_TYPE   = { "int" }
FLOAT_TYPE = { "float" }
STR_TYPE   = { "string" }
VOID_TYPE  = { "void" }

/// ### Literals
/// Include literals such as booleans, integers, floats, strings, and null.
literal =  { BOOL | FLOAT | INT | STR | NULL }
BOOL    = @{ TRUE_KW | FALSE_KW }
INT     = @{ ASCII_DIGIT+ }
FLOAT   = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
STR     = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
NULL    = @{ NULL_KW }

/// ### Identifiers
/// Identifiers are used for naming variables, functions, and other constructs.
identifier        = { IDENT }
reserved_keywords = { (KEYWORDS | datatype) ~ !IDENT_CHAR }

IDENT      = @{ !reserved_keywords ~ ASCII_ALPHA ~ IDENT_CHAR* }
IDENT_CHAR = _{ ASCII_ALPHANUMERIC | "_" }

/// ## Whitespace and Comments
/// Whitespace and comments are ignored by the parser.
WHITESPACE = _{ " " | "\t" | NEWLINE }
NEWLINE    = _{ "\n" | "\r" | "\r\n" }

COMMENT        = _{ INLINE_COMMENT | BLOCK_COMMENT }
INLINE_COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }
BLOCK_COMMENT  = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
